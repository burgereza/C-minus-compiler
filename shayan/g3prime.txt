Program -> Declaration-list $
Declaration-list -> Declaration Declaration-list
Declaration-list -> ε
Declaration -> Declaration-initial Declaration-prime
Declaration-initial -> Type-specifier #push ID #push
Declaration-prime -> #function_start Fun-declaration-prime
Declaration-prime -> Var-declaration-prime
Var-declaration-prime -> #define_variable ;
Var-declaration-prime -> [ NUM #push #define_array ] ;
Fun-declaration-prime -> ( Params ) #define_function Compound-stmt #end_of_scope #end_of_function
Type-specifier -> int
Type-specifier -> void
Params -> int #push ID #push Param-prime Param-list
Params -> void Param-list-void-abtar
Param-list-void-abtar -> ID Param-prime Param-list
Param-list-void-abtar -> ε
Param-list -> , Param Param-list
Param-list -> ε
Param -> Declaration-initial Param-prime
Param-prime -> [ ] #push_array_type
Param-prime -> ε #push_non_type
Compound-stmt -> { Declaration-list Statement-list }
Statement-list -> Statement Statement-list
Statement-list -> ε
Statement -> Expression-stmt
Statement -> Compound-stmt
Statement -> Selection-stmt
Statement -> Iteration-stmt
Statement -> Return-stmt
Statement -> Switch-stmt
Expression-stmt -> Expression #pop_number ;
Expression-stmt -> break #break_command ;
Expression-stmt -> ;
Selection-stmt -> if ( Expression #start_if ) Statement else #start_else Statement #end_if
Iteration-stmt -> while ( #start_while Expression ) #while_condition Statement #end_while
Return-stmt -> return Return-stmt-prime
Return-stmt-prime -> #function_return ;
Return-stmt-prime -> Expression #function_return_with_value ;
Switch-stmt -> switch #start_switch ( Expression ) { Case-stmts Default-stmt } #end_switch
Case-stmts -> Case-stmt Case-stmts
Case-stmts -> ε
Case-stmt -> case NUM #push_number #start_case : Statement-list #end_case
Default-stmt -> default : Statement-list
Default-stmt -> ε
Expression -> Simple-expression-zegond
Expression -> ID #push_id B
B -> = Expression #assign
B -> [ Expression ] #find_array_index_address H
B -> Simple-expression-prime
H -> = Expression #assign
H -> G D C
Simple-expression-zegond -> Additive-expression-zegond C
Simple-expression-prime -> Additive-expression-prime C
C -> Relop Additive-expression #add_or_sub_or_compare
C -> ε
Relop -> < #LT
Relop -> == #EQ
Additive-expression -> Term D
Additive-expression-prime -> Term-prime D
Additive-expression-zegond -> Term-zegond D
D -> Addop Term #add_or_sub_or_compare D
D -> ε
Addop -> + #add_values
Addop -> - #sub_values
Term -> Signed-factor G
Term-prime -> Signed-factor-prime G
Term-zegond -> Signed-factor-zegond G
G -> * Signed-factor #mult_values G
G -> ε
Signed-factor -> + Factor
Signed-factor -> - Factor #change_sign
Signed-factor -> Factor
Signed-factor-prime -> Factor-prime
Signed-factor-zegond -> + Factor
Signed-factor-zegond -> - Factor #change_sign
Signed-factor-zegond -> Factor-zegond
Factor -> ( Expression )
Factor -> ID #push_id Var-call-prime
Factor -> NUM #push_number
Var-call-prime -> #function_call_start ( Args ) #function_call
Var-call-prime -> Var-prime
Var-prime -> [ Expression ] #find_array_index_address
Var-prime -> ε
Factor-prime -> #function_call_start ( Args ) #function_call
Factor-prime -> ε
Factor-zegond -> ( Expression )
Factor-zegond -> NUM #push_number
Args -> Arg-list
Args -> ε
Arg-list -> Expression Arg-list-prime
Arg-list-prime -> , Expression Arg-list-prime
Arg-list-prime -> ε
